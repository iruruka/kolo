int Nx=128;
mesh Th = square(Nx,Nx);
fespace Vh(Th,P1);
fespace Uh(Th,P0);

int Ntmax=200;
real T=1.0e-2, tau=sqrt(2)/256, dt, epsilon=0.9, kappa, time=0.0, eps=1.0e-10;
real Du=1.0, Dv=1.0, K1=0.1, K2=0.01, K=0.1;
macro phi(v) 5*v^2 //


real a=95.0;
//real x1=0.3,y1=0.3,x2=0.7,y2=0.3,x3=0.5,y3=0.7;
//func u0 = 1*exp(-a*(x-x1)^2-a*(y-y1)^2) + 1*exp(-a*(x-x2)^2-a*(y-y2)^2) ;
//func v0 = 1*exp(-a*(x-x3)^2-a*(y-y3)^2);
real x1=0.5,y1=1.0/3.0,x2=1.0/2.0+sqrt(3)/12.0,y2=1.0/2.0+1.0/12.0,x3=1.0/2.0-sqrt(3)/12.0,y3=1.0/2.0+1.0/12.0,x4=0.5,y4=0.5;
//real x1=0.5,y1=2.0/9.0,x2=1.0/2.0+5*sqrt(3)/36.0,y2=1.0/2.0+5.0/36.0,x3=1.0/2.0-5*sqrt(3)/36.0,y3=1.0/2.0+5.0/36.0,x4=0.5,y4=0.5;
func u0 = 500*exp(-a*(x-x1)^2-a*(y-y1)^2) + 500*exp(-a*(x-x2)^2-a*(y-y2)^2) + 500*exp(-a*(x-x3)^2-a*(y-y3)^2);
func v0 = 1*exp(-1*(x-x4)^2-1*(y-y4)^2);

Vh uh,vh,ph,qh,uh1,vh1,phih;
Uh dphihx,dphihy;


varf massM(ph,qh) = int2d(Th,qft=qf1pTlump)(ph*qh);
varf stiffM(ph,qh) = int2d(Th)(dx(ph)*dx(qh)+dy(ph)*dy(qh));

matrix M = massM(Vh,Vh);
matrix A = stiffM(Vh,Vh);
matrix B(Vh.ndof,Vh.ndof);

real[int] mass(Ntmax), times(Ntmax);

// geometric information
int i,j,j1,j2,k1,k2,itr=0;
real[int] MV(2);
real[int,int] geom(Th.nt,9);
real Area,Len;
kappa = 1.0;

for(int t=0; t<Th.nt; t++){
    Area = Th[t].area;
    for(int k=0; k<3; k++){
        k1 = (k+1)%3;
        k2 = (k+2)%3;
        MV[0] = (Th[t][k1].x + Th[t][k2].x)/2.0 - Th[t][k].x;
        MV[1] = (Th[t][k1].y + Th[t][k2].y)/2.0 - Th[t][k].y;
        geom(t,k) = MV.l2/3.0;
        geom(t,2*k+3) = -MV[1]/MV.l2;
        geom(t,2*k+4) =  MV[0]/MV.l2;

        Len = sqrt( (Th[t][k1].x - Th[t][k2].x)^2 + (Th[t][k1].y - Th[t][k2].y)^2 );
        kappa = min(kappa, Area/Len*2);
    }
}




matrix Coeff(Vh.ndof,Vh.ndof);
real[int] rhs(Vh.ndof),rhs1(Vh.ndof);


uh1 = u0;
vh1 = v0;
real gammaij1,gammaij2,nuij1x,nuij1y,nuij2x,nuij2y,temp1,temp2,dphiNorm;

while(time<T && itr<Ntmax){

    
    mass[itr] = int2d(Th,qft=qf1pTlump)(uh1);
    times[itr] = time;

    plot(uh1,dim=3,fill=true,value=true,cmm="t = "+time+", iteration: "+itr);
    // plot(vh1,fill=true,value=true);

    phih = phi(vh1);
    dphihx = dx(phih);
    dphihy = dy(phih);
    dphiNorm = 0.0;

    // make coefficient matrix and RHS
    for(int t=0; t<Th.nt; t++){
        dphiNorm = max( dphiNorm, sqrt(dphihx[][t]^2 + dphihy[][t]^2) );
        for(int k=0; k<3; k++){
            k1 = (k+1)%3;
            k2 = (k+2)%3;
            i = Th[t][k];
            j1 = Th[t][k1];
            j2 = Th[t][k2];
            gammaij1 = geom(t,k2);
            gammaij2 = geom(t,k1);
            nuij1x =  geom(t,2*k2+3);
            nuij1y =  geom(t,2*k2+4);
            nuij2x = -geom(t,2*k1+3);
            nuij2y = -geom(t,2*k1+4);

            temp1 = nuij1x*dphihx[][t] + nuij1y*dphihy[][t];
            temp2 = nuij2x*dphihx[][t] + nuij2y*dphihy[][t];
            if(temp1>0){
                B(i,i) += gammaij1 * temp1;
            }else{
                B(i,j1) += gammaij1 * temp1;
            }
            if(temp2>0){
                B(i,i) += gammaij2 * temp2;
            }else{
                B(i,j2) += gammaij2 * temp2;
            }
        }
    }
    // Comupute time increment
    dt = dphiNorm<eps ? tau : min(tau, epsilon*kappa/(4*dphiNorm) );

    // Update u & v
    Coeff = M + dt*Du*A + dt*B;
    set(Coeff, solver=sparsesolver);
    rhs = M*uh1[];
    uh[] = Coeff^-1*rhs;

    Coeff = (1.0 + dt*K1/K)*M + dt/K*Dv*A;
    set(Coeff, solver=sparsesolver);
    rhs1 = vh1[] + dt*K2/K*uh[];
    rhs = M*rhs1;
    vh[] = Coeff^-1*rhs;


    uh1 = uh;
    vh1 = vh;
    real mass = int2d(Th)(uh1);
    real umin = uh1[].min;
    
    // coutで20桁の精度を設定
    cout.precision(20);
    cout << "mass = " << mass << ", min = " << umin << endl;

    time += dt;
    itr += 1;

}
/*
ofstream tt("mass_adaptive.csv");
tt.precision(16);
for(int l=0;l<Ntmax;l++){
    tt << times[l] << "," << mass[l] << endl;
}
*/
