int Nx=32;
mesh Th = square(Nx,Nx);
//mesh Th = square(Nx, Nx, [-0.5 + x, -0.5 + y]);
fespace Vh(Th,P1);
fespace Uh(Th,P0);

int Ntmax=100;
//real T=1.0e-1, tau=sqrt(2)/256, dt, epsilon=0.9, kappa, time=0.0, eps=1.0e-10;
real T=1.0e-1, tau=1.0e-6, dt, epsilon=0.9, kappa, time=0.0, eps=1.0e-10;
real d1=1.0,d2=1.0,alpha=1.0,beta=1.0,chi=1.0,mu=1;
macro phi(v) v //

real b = 75.0;
real r = 0.2;
real x1 = 0.5 + r*cos(0.0);
real y1 = 0.5 + r*sin(0.0);
real x2 = 0.5 + r*cos(2*pi/3);
real y2 = 0.5 + r*sin(2*pi/3);
real x3 = 0.5 + r*cos(4*pi/3);
real y3 = 0.5 + r*sin(4*pi/3);
func u0 = 300*exp(-b*(x - x1)^2 - b*(y - y1)^2)
        + 300*exp(-b*(x - x2)^2 - b*(y - y2)^2)
        + 300*exp(-b*(x - x3)^2 - b*(y - y3)^2);
func v0 = 100 * exp(-50* ((x-0.5)^2 + (y-0.5)^2));
/*
func u0 = 1000 * exp(-100 * ((x-0.5)^2 + (y-0.5)^2));
func v0 = 500 * exp(-50 * ((x-0.5)^2 + (y-0.5)^2));
*/
Vh uh,vh,ph,qh,uh1,vh1,phih;
Uh dphihx,dphihy;


varf massM(ph,qh) = int2d(Th,qft=qf1pTlump)(ph*qh);
varf stiffM(ph,qh) = int2d(Th)(dx(ph)*dx(qh)+dy(ph)*dy(qh));
//varf convM(ph,qh) = int2d(Th)(ph*(dx(vh1)*dx(qh)+dy(vh1)*dy(qh)));

matrix M = massM(Vh,Vh);
matrix A = stiffM(Vh,Vh);
//matrix K = convM(Vh,Vh);

real[int] mass(Ntmax), times(Ntmax);

// geometric information
int i,j,j1,j2,k1,k2,itr=1;
real[int] MV(2);
real[int,int] geom(Th.nt,9);
real Area,Len;
kappa = 1.0;

for(int t=0; t<Th.nt; t++){
    Area = Th[t].area;
    for(int k=0; k<3; k++){
        k1 = (k+1)%3;
        k2 = (k+2)%3;
        MV[0] = (Th[t][k1].x + Th[t][k2].x)/2.0 - Th[t][k].x;
        MV[1] = (Th[t][k1].y + Th[t][k2].y)/2.0 - Th[t][k].y;
        geom(t,k) = MV.l2/3.0;
        geom(t,2*k+3) = -MV[1]/MV.l2;
        geom(t,2*k+4) =  MV[0]/MV.l2;

        Len = sqrt( (Th[t][k1].x - Th[t][k2].x)^2 + (Th[t][k1].y - Th[t][k2].y)^2 );
        kappa = min(kappa, Area/Len*2);
    }
}




matrix Coeff(Vh.ndof,Vh.ndof);
real[int] rhs(Vh.ndof),rhs1(Vh.ndof),rhs2(Vh.ndof);


uh1 = u0;
vh1 = v0;
real gammaij1,gammaij2,nuij1x,nuij1y,nuij2x,nuij2y,temp1,temp2,dphiNorm;

// 結果を保存するCSVファイルをオープン
//ofstream massvalue("massvalue-ad.csv");
//massvalue << "Step, Mass, Min\n"; // ヘッダーを追加

while(time<T && itr<Ntmax){

    
    mass[itr] = int2d(Th,qft=qf1pTlump)(uh1);
    times[itr] = time;

    plot(uh1,dim=3,fill=true,value=true,cmm="t = "+time+", iteration: "+itr);
    // plot(vh1,fill=true,value=true);

    phih = phi(vh1);
    dphihx = dx(phih);
    dphihy = dy(phih);
    dphiNorm = 0.0;
    matrix B(Vh.ndof,Vh.ndof);
    // make coefficient matrix and RHS
    for(int t=0; t<Th.nt; t++){
        dphiNorm = max( dphiNorm, sqrt(dphihx[][t]^2 + dphihy[][t]^2) );
        for(int k=0; k<3; k++){
            k1 = (k+1)%3;
            k2 = (k+2)%3;
            i = Th[t][k];
            j1 = Th[t][k1];
            j2 = Th[t][k2];
            gammaij1 = geom(t,k2);
            gammaij2 = geom(t,k1);
            nuij1x =  geom(t,2*k2+3);
            nuij1y =  geom(t,2*k2+4);
            nuij2x = -geom(t,2*k1+3);
            nuij2y = -geom(t,2*k1+4);

            temp1 = nuij1x*dphihx[][t] + nuij1y*dphihy[][t];
            temp2 = nuij2x*dphihx[][t] + nuij2y*dphihy[][t];
            if(temp1>0){
                B(i,i) += gammaij1 * temp1;
            }else{
                B(i,j1) += gammaij1 * temp1;
            }
            if(temp2>0){
                B(i,i) += gammaij2 * temp2;
            }else{
                B(i,j2) += gammaij2 * temp2;
            }
        }
    }
    // Comupute time increment
    dt = dphiNorm<eps ? tau : min(tau, epsilon*kappa/(4*dphiNorm) );

    // Update u & v
    Coeff = M + dt*d1*A + dt*chi*B;
    set(Coeff, solver=sparsesolver);
    rhs = M*uh1[];
    uh[] = Coeff^-1*rhs;
    //FCT
    int i,k1,k2,r1,r2;
    matrix C(Vh.ndof, Vh.ndof);
    matrix F(Vh.ndof, Vh.ndof);
    matrix a(Vh.ndof, Vh.ndof);
    matrix fa(Vh.ndof, Vh.ndof);
    varf convM(ph,qh) = int2d(Th)(-ph*(dx(vh1)*dx(qh)+dy(vh1)*dy(qh)));
    matrix K = convM(Vh,Vh);
    matrix B2(Vh.ndof, Vh.ndof);

    real[int] Pp(Vh.ndof),Pn(Vh.ndof),Qp(Vh.ndof),Qn(Vh.ndof),Rp(Vh.ndof),Rn(Vh.ndof),Fa(Vh.ndof);
    C=B-K;

/*    ofstream csvFile("matrix_C.csv");

    // 行列aの各要素を出力。各行はカンマ区切りにしています。
    for (int i = 0; i < Vh.ndof; i++) {
        for (int j = 0; j < Vh.ndof; j++) {
            csvFile << C(i, j);
            if (j < Vh.ndof - 1) csvFile << ",";
        }
        csvFile << "\n";
    }*/

    // ※ 前提：Vh.ndof は自由度の数、uh はベクトル（例えば uh[][i] で i 番目の値）、
// C, F, a, fa は ndof×ndof の行列、M は対角成分が利用可能な質量行列、dt は時間刻み幅、
// Pp, Pn, Rp, Rn, Qp, Qn, Fa は長さ ndof のベクトル、Th はメッシュの要素（各要素3つのノード番号）

// 1. Fの計算と条件によるゼロ化を同時に実施
for (int i = 0; i < Vh.ndof; i++) {
    for (int j = 0; j < Vh.ndof; j++) {
        if (i == j) continue; // 対角項はスキップ
        real diff = uh[][j] - uh[][i];
        // ここでは、抗拡散フラックスとして定義するために符号反転
        real temp = - C(i, j) * diff;
        // 抗拡散であるなら (temp と diff が反対の符号)
        // つまり、temp*diff < 0 なら抗拡散フラックスとして残す
        F(i, j) = (temp * diff < 0) ? temp : 0;
    }
}


// 2. 各ノード i に対して Pp[i] と Pn[i] を一度のループで算出
for (int i = 0; i < Vh.ndof; i++) {
    Pp[i] = 0;
    Pn[i] = 0;
    for (int j = 0; j < Vh.ndof; j++) {
        if(i == j) continue;
        Pp[i] += max(0.0, F(i, j));
        Pn[i] += min(0.0, F(i, j));
    }
}

// 3. 三角形（要素）情報から Qp と Qn を更新
// ここでは、各自由度 i の初期値を十分小／大な値にセットしておく
for (int i = 0; i < Vh.ndof; i++) {
    Qp[i] = -1e30; // 非常に小さい値
    Qn[i] = 1e30;  // 非常に大きい値
}
for (int t = 0; t < Th.nt; t++) {
    for (int k = 0; k < 3; k++) {
        int i = Th[t][k];
        for (int m = 0; m < 3; m++) {
            if(m == k) continue;
            int j = Th[t][m];
            real diff = uh[][j] - uh[][i];
            if(diff > Qp[i]) Qp[i] = diff;
            if(diff < Qn[i]) Qn[i] = diff;
        }
    }
}

// 4. 各ノードごとに補正係数 Rp, Rn を計算
for (int i = 0; i < Vh.ndof; i++) {
    Rp[i] = (Pp[i] != 0) ? min(1.0, M(i, i) * Qp[i] / (dt * Pp[i])) : 1.0;
    Rn[i] = (Pn[i] != 0) ? min(1.0, M(i, i) * Qn[i] / (dt * Pn[i])) : 1.0;
}

// 5. a(i,j) の計算（Fの正負によって場合分け）
for (int i = 0; i < Vh.ndof; i++) {
    for (int j = 0; j < Vh.ndof; j++) {
        if(i == j) continue;
        if (F(i, j) > 0) {
            a(i, j) = min(Rp[i], Rn[j]);
        } else {
            a(i, j) = min(Rp[j], Rn[i]);
        }
    }
}

// 6. 限界付きフラックス fa を計算
for (int i = 0; i < Vh.ndof; i++) {
    for (int j = 0; j < Vh.ndof; j++) {
        fa(i, j) = a(i, j) * F(i, j);
    }
}

// 7. 各ノード i の総合フラックス Fa[i] を算出
for (int i = 0; i < Vh.ndof; i++) {
    Fa[i] = 0;
    for (int j = 0; j < Vh.ndof; j++) {
        if(i == j) continue;
        Fa[i] += fa(i, j);
    }
}

/*
    ofstream fout("vector.csv");
    for (int i = 0; i < Vh.ndof; i++) {
        fout << Fa[i];
        if (i < Vh.ndof-1) {
            fout << ",";  // 最後の要素でなければカンマを出力
        }
    }
*/
    Coeff = M + dt*d1*A + dt*chi*B;
    set(Coeff, solver=sparsesolver);
    rhs = M*uh1[];
    rhs += Fa;
    uh[] = Coeff^-1*rhs;

    Coeff = (1.0 + dt*beta)*M + dt*d2*A;
    set(Coeff, solver=sparsesolver);
    rhs1 = vh1[] + dt*alpha*uh[];
    rhs = M*rhs1;
    vh[] = Coeff^-1*rhs;


    uh1 = uh;
    vh1 = vh;

    real mass = int2d(Th)(uh1);
    real umin = uh1[].min;
  
    // coutで20桁の精度を設定
    cout.precision(20);
    cout << "mass = " << mass << ", min = " << umin << endl;

    time += dt;
    itr += 1;

}
